// Copyright 2019-2020 LinkedIn Corporation. All rights reserved.
// Licensed under the BSD-2 Clause license.
// See LICENSE in the project root for license information.

plugins {
  id "com.diffplug.spotless" version "5.8.2"
  id 'com.github.johnrengelman.shadow' version '2.0.4'
  id "org.shipkit.java" version "2.3.4"
}

configurations {
  provided
}

allprojects {
  group = "com.linkedin.coral"
  apply plugin: "com.diffplug.spotless"
  apply plugin: "idea"
  apply plugin: "eclipse"

  repositories {
    mavenCentral()
    maven {
      url 'https://linkedin.bintray.com/maven/'
    }
  }

  spotless {
    enforceCheck true
    groovyGradle {
      target '**/*.gradle'
      greclipse().configFile("${rootDir}/docs/codestyle-eclipse.xml")
    }
    format 'markdown', {
      target 'CONTRIBUTING.md', 'README.md', 'docs/*.md'
      targetExclude 'docs/release-notes.md'
      prettier()
    }
  }
}

subprojects {
  plugins.withType(JavaPlugin) {
    dependencies {
      testCompile deps.'testing'
    }
    test {
      useTestNG()
    }
    spotless {
      java {
        eclipse().configFile("${rootDir}/docs/codestyle-eclipse.xml")
        licenseHeaderFile file("${rootDir}/gradle/license/LICENSE_HEADER")
      }
    }
  }

  apply from: "${rootDir}/gradle/dependencies.gradle"
}
